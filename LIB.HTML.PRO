PROCEDURE CREATEPAGE
 HTMLVIEWOPEN
  CALL GETHTML
 HTMLVIEWDISPLAY
 CALL PARSEHTML
END [CREATEPAGE]


PROCEDURE GETHTML
 IF HTMLSKINNAME<>"" THEN
  DO
   FILEOPEN("REPGEN",HTMLSKINNAME,"READ",HTMLFNUMBER,HTMLFERROR) 
   IF (HTMLFERROR<>"") THEN
    POPUPMESSAGE(2,"ERROR OPENING SKIN FILE: "+HTMLFNAME+" "+HTMLFERROR)
   HTMLFLINE=""
   WHILE (HTMLFERROR="" AND
          CHARACTERSEARCH(HTMLFLINE,"[*")=0)
    DO
     FILEREADLINE(HTMLFNUMBER,HTMLFLINE,HTMLFERROR)      
     IF (CHARACTERSEARCH(HTMLFLINE,"[*")=0) THEN
       HTMLVIEWLINE(HTMLFLINE)     
    END
   FILECLOSE(HTMLFNUMBER,HTMLFERROR)  
  END
 
 FILEOPEN("REPGEN",HTMLFNAME,"READ",HTMLFNUMBER,HTMLFERROR) 
 IF (HTMLFERROR<>"") THEN
  POPUPMESSAGE(2,"ERROR OPENING HTML FILE: "+HTMLFNAME+" "+HTMLFERROR)
 WHILE (CHARACTERSEARCH(HTMLFLINE,"[*"+HTMLPAGENAME)=0)
  DO
   FILEREADLINE(HTMLFNUMBER,HTMLFLINE,HTMLFERROR)
  END
 HTMLFLINE=""
 WHILE (HTMLFERROR="" AND
        CHARACTERSEARCH(HTMLFLINE,"[*")=0)
  DO
   FILEREADLINE(HTMLFNUMBER,HTMLFLINE,HTMLFERROR)
   IF (CHARACTERSEARCH(HTMLFLINE,"[*")=0) THEN
    DO
     IF (CHARACTERSEARCH(HTMLFLINE,"[!")>0) THEN
      DO
       HTMLPOS1=CHARACTERSEARCH(HTMLFLINE,"[!")
       HTMLPOS2=CHARACTERSEARCH(HTMLFLINE,"]")
       HTMLVIEWLINE(SEGMENT(HTMLFLINE,1,HTMLPOS1-1))
       HTMLREPLACENAME=SEGMENT(HTMLFLINE,HTMLPOS1+2,HTMLPOS2-1)
       CALL REPLACEHTML
       HTMLVIEWLINE(SEGMENT(HTMLFLINE,HTMLPOS2+1,LENGTH(HTMLFLINE)))
      END
     ELSE
      HTMLVIEWLINE(HTMLFLINE)
    END
  END
 FILECLOSE(HTMLFNUMBER,HTMLFERROR)

 IF HTMLSESSION=1 THEN
  DO
   HTMLSESHFNAME="HTMLSESH."+HTMLPAGENAME+FORMAT(".9999",SYSCONSOLENUM)+FORMAT(".999999",SYSTEMDATE)  
   FILEOPEN("LETTER",HTMLSESHFNAME,"READ",HTMLFNUMBER,HTMLFERROR)
   IF HTMLFERROR="" THEN
    DO   
     HTMLVIEWLINE("<script type='text/javascript'>")
     WHILE HTMLFERROR=""
      DO
       FILEREADLINE(HTMLFNUMBER,HTMLFLINE,HTMLFERROR) 
       IF HTMLFLINE<>"" THEN
        DO
         HTMLPOS1=CHARACTERSEARCH(HTMLFLINE,"=")
         HTMLINPUTNAME=SEGMENT(HTMLFLINE,1,HTMLPOS1-1)
         HTMLINPUTVALUE=SEGMENT(HTMLFLINE,HTMLPOS1+1,LENGTH(HTMLFLINE))     
         IF SEGMENT(HTMLINPUTNAME,1,2)="cb" THEN
          DO
           HTMLVIEWLINE("  "+HTMLFORMNAME+"."+HTMLINPUTNAME+".checked=true;")
          END
         ELSE
         IF SEGMENT(HTMLINPUTNAME,1,2)="rb" THEN
          DO
           HTMLVIEWLINE("for (i=0;i<"+HTMLFORMNAME+"."+HTMLINPUTNAME+".length;i++) {")
           HTMLVIEWLINE(" if ("+HTMLFORMNAME+"."+HTMLINPUTNAME+"[i].value=="+CTRLCHR(34)+HTMLINPUTVALUE+CTRLCHR(34)+") {")
           HTMLVIEWLINE("  "+HTMLFORMNAME+"."+HTMLINPUTNAME+"[i].checked=true;")
           HTMLVIEWLINE(" }")
           HTMLVIEWLINE("}")
          END
         ELSE
          HTMLVIEWLINE("  "+HTMLFORMNAME+"."+HTMLINPUTNAME+".value"+"="+CTRLCHR(34)+HTMLINPUTVALUE+CTRLCHR(34)+";")
        END
      END
     FILECLOSE(HTMLFNUMBER,HTMLFERROR)      
     FILEDELETE("LETTER",HTMLFNAME,HTMLFERROR)
     HTMLVIEWLINE("</script>")
    END
  END

 HTMLFLINE=""
 IF HTMLSKINNAME<>"" THEN
  DO
   FILEOPEN("REPGEN",HTMLSKINNAME,"READ",HTMLFNUMBER,HTMLFERROR) 
   IF (HTMLFERROR<>"") THEN
    POPUPMESSAGE(2,"ERROR OPENING SKIN FILE: "+HTMLFNAME+" "+HTMLFERROR)
   WHILE (CHARACTERSEARCH(HTMLFLINE,"[*")=0)
    DO
     FILEREADLINE(HTMLFNUMBER,HTMLFLINE,HTMLFERROR)
    END      
   HTMLFLINE=""
   WHILE (HTMLFERROR="")
    DO
     FILEREADLINE(HTMLFNUMBER,HTMLFLINE,HTMLFERROR)
     HTMLVIEWLINE(HTMLFLINE)     
    END
   FILECLOSE(HTMLFNUMBER,HTMLFERROR)  
  END 
END [GETHTML]


PROCEDURE PARSEHTML
 IF HTMLSESSION=1 THEN
  DO
   HTMLSESHFNAME="HTMLSESH."+HTMLPAGENAME+FORMAT(".9999",SYSCONSOLENUM)+FORMAT(".999999",SYSTEMDATE)
   FILEDELETE("LETTER",HTMLSESHFNAME,HTMLSESHFERROR)
   FILEOPEN("LETTER",HTMLSESHFNAME,"WRITE",HTMLSESHFNUMBER,HTMLSESHFERROR)   
  END
 HTMLFLINE=""
 WHILE (HTMLFLINE<>"EOD")
  DO
   HTMLFLINE      = ENTERLINE(0)
   HTMLPOS1       = CHARACTERSEARCH(HTMLFLINE,"=")
   HTMLINPUTNAME  = SEGMENT(HTMLFLINE,1,HTMLPOS1-1)
   HTMLINPUTVALUE = SEGMENT(HTMLFLINE,HTMLPOS1+1,LENGTH(HTMLFLINE))
   IF HTMLSESSION=1 AND HTMLFLINE<>"EOD" AND UPPERCASE(HTMLINPUTNAME)<>"HTMLPAGENAME" THEN
    FILEWRITELINE(HTMLSESHFNUMBER,HTMLINPUTNAME+"="+HTMLINPUTVALUE,HTMLSESHFERROR)
   CALL SETVARIABLES
  END
 IF HTMLSESSION=1 THEN
  FILECLOSE(HTMLSESHFNUMBER,HTMLSESHFERROR)
END [PARSEHTML]


PROCEDURE STARTSESSION
 HTMLSESSION=1
 FILELISTOPEN("LETTER","HTMLSESH+"+FORMAT(".9999",SYSCONSOLENUM)+"+",HTMLFERROR)
 WHILE HTMLFERROR="" 
  DO 
   FILELISTREAD(HTMLSESHFNAME,HTMLFERROR) 
   FILEDELETE("LETTER",HTMLSESHFNAME,HTMLFERROR)
  END
END [STARTSESSION]


